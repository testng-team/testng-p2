buildscript {
	repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
	}
	dependencies {
		classpath 'org.standardout:bnd-platform:1.6.0-SNAPSHOT'
        classpath 'com.github.missedone:gradle-bintray-p2-plugin:1.1.0'
	}
}

def buildTime() {
	def df = new java.text.SimpleDateFormat("yyyyMMddHHmm")
	df.setTimeZone(TimeZone.getTimeZone("UTC"))
	return df.format(new Date())
}

apply plugin: 'org.standardout.bnd-platform'
apply plugin: 'bintray-p2'

repositories {
	mavenCentral()
	jcenter()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

// project version is also platform feature version default,
// here uses TestNG version as project version.
version = '6.11.1-SNAPSHOT'

def buildMode = System.getProperty('BUILD_MODE')
def versionWithQualifier

def qualifierPrefix = 'b'       // 'b' for Beta
if (buildMode == "release") {
    assert !version.endsWith('-SNAPSHOT') : "Please don't use snapshot version ${version} for release"
    qualifierPrefix = 'r'       // 'r' for Release
}

if (version ==~ /^\d+[\.\d+]+$/) {
    versionWithQualifier = version + "." + qualifierPrefix + buildTime()
}
else {
    versionWithQualifier = version.replaceAll('-SNAPSHOT', "." + qualifierPrefix + buildTime())
}

defaultTasks 'updateSiteZip'

updateSiteZip.dependsOn clean

platform {
    bundle "org.testng:testng:${version}", {
        bnd {
            // instruction 'Export-Package', "org.testng.*;version=$version"
            instruction 'Import-Package', 
                                            'com.beust.jcommander.*;version="[1.7.0,2.0.0)",' + 
                                            'bsh.*;version="[2.0.0,3.0.0)";resolution:=optional,' +
                                            'com.google.inject.*;version="[1.2,1.3)";resolution:=optional,' +
                                            'junit.framework;version="[3.8.1, 5.0.0)";resolution:=optional,' +
                                            'org.junit.*;resolution:=optional,' +
                                            'org.apache.tools.ant.*;version="[1.7.0, 2.0.0)";resolution:=optional,' +
                                            'org.yaml.*;version="[1.6,2.0)";resolution:=optional,' +
                                            '!com.beust.testng,' +
                                            '!org.testng.*,' +
                                            '!com.sun.*'
            instruction 'Require-Bundle', 'com.beust.jcommander;bundle-version="[1.7.0,2.0.0)";visibility:=reexport'
            instruction 'Bundle-Vendor', 'TestNG Team'
            version = versionWithQualifier
        }
        exclude module: 'ant'
        exclude module: 'bsh'
    }

    bundle "org.yaml:snakeyaml:1.17"

    bundle "org.apache-extras.beanshell:bsh:2.0b6", {
        bnd {
            optionalImport 'org.apache.bsf.*'
        }
    }

    featureId 'org.testng.p2.feature'
    featureName 'TestNG P2 Feature'
    featureVersion versionWithQualifier
    featureProvider 'TestNG Team'

    categoryName 'TestNG P2 Libraries'
    categoryId 'org.testng.p2.libraries'

    useBndHashQualifiers false
    useFeatureHashQualifiers false
    defaultQualifier ''

    downloadsDir = new File(System.properties['user.home'], '.bnd-eclipse')
    eclipseMirror.linux.gtk.x86_64 = 'http://download.eclipse.org/eclipse/downloads/drops4/R-4.6.3-201703010400/eclipse-platform-4.6.3-linux-gtk-x86_64.tar.gz'
    eclipseMirror.macosx.cocoa.x86_64 = 'http://download.eclipse.org/eclipse/downloads/drops4/R-4.5.2-201602121500/eclipse-platform-4.5.2-macosx-cocoa-x86_64.tar.gz'
}

publishP2Repo {
	repoOwner = 'testng-team'
    repoName = buildMode == 'release' ? 'testng-p2-release' : 'testng-p2'
    compositePackage = ''
    subCompositeStrategy = 'MICRO'
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.13'
}
