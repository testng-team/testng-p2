buildscript {
	repositories {
        mavenLocal()
        jcenter()
	}
	dependencies {
		classpath 'org.standardout:bnd-platform:1.5.0'
        classpath 'com.github.missedone:gradle-bintray-p2-plugin:1.1.0'
	}
}

def buildTime() {
	def df = new java.text.SimpleDateFormat("yyyyMMddHHmmss")
	df.setTimeZone(TimeZone.getTimeZone("UTC"))
	return df.format(new Date())
}

apply plugin: 'org.standardout.bnd-platform'
apply plugin: 'bintray-p2'

repositories {
	mavenCentral()
	jcenter()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

// project version is also platform feature version default,
// here uses TestNG version as project version.
version = '6.9.13-SNAPSHOT'

def versionWithQualifier = version.replaceAll('-SNAPSHOT', "." + buildTime())

defaultTasks 'updateSiteZip'

updateSiteZip.dependsOn clean

platform {
    override {
        optionalImport 'javax.*'
    }
    bundle "org.testng:testng:${version}", {
        bnd {
            optionalImport 'com.google.common.*'
            optionalImport 'org.w3c.*'
            optionalImport 'org.xml.*'
            version = versionWithQualifier
        }
        exclude module: 'ant'
    }
    bnd (name: 'bsh') {
        optionalImport 'org.apache.bsf.*'
    }

    featureId 'org.testng.p2.feature'
    featureName 'TestNG P2 Feature'
    featureVersion versionWithQualifier
    featureProvider 'TestNG Team'

    categoryName 'TestNG P2 Libraries'
    categoryId 'org.testng.p2.libraries'

    useBndHashQualifiers false
    useFeatureHashQualifiers false
    defaultQualifier ''

    downloadsDir = new File(System.properties['user.home'], '.bnd-eclipse')
    eclipseMirror.linux.gtk.x86_64 = 'http://download.eclipse.org/eclipse/downloads/drops4/R-4.5.2-201602121500/eclipse-platform-4.5.2-linux-gtk-x86_64.tar.gz'
    eclipseMirror.macosx.cocoa.x86_64 = 'http://download.eclipse.org/eclipse/downloads/drops4/R-4.5.2-201602121500/eclipse-platform-4.5.2-macosx-cocoa-x86_64.tar.gz'
}

publishP2Repo {
	repoOwner = 'testng-team'
	repoName = 'testng-p2-beta'
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.13'
}
