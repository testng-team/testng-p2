buildscript {
	repositories {
        jcenter()
	}
	dependencies {
		classpath 'org.standardout:bnd-platform:1.4.0'

        // see https://github.com/michel-kraemer/gradle-download-task/issues/56
        classpath 'de.undercouch:gradle-download-task:3.1.1'
        classpath 'org.apache.httpcomponents:httpclient:4.5.2'
	}
}

def buildTime() {
	def df = new java.text.SimpleDateFormat("yyyyMMddHHmmss")
	df.setTimeZone(TimeZone.getTimeZone("UTC"))
	return df.format(new Date())
}

apply plugin: 'org.standardout.bnd-platform'
apply plugin: org.testng.p2.gradle.BintrayP2Plugin

repositories {
	mavenCentral()
	jcenter()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

// project version is also platform feature version default,
// here uses TestNG version as project version.
version = '6.9.13-SNAPSHOT'

def versionWithQualifier = version.replaceAll('-SNAPSHOT', "." + buildTime())

defaultTasks 'updateSiteZip'

updateSiteZip.dependsOn clean

platform {
    bundle "org.testng:testng:${version}", {
        bnd {
            optionalImport 'com.google.common.*'
            version = versionWithQualifier
        }
        exclude module: 'ant'
    }

    featureId 'org.testng.p2.feature'
    featureName 'TestNG Feature'
    featureVersion versionWithQualifier

    categoryName 'TestNG Libraries'
    categoryId 'org.testng.p2.libraries'

    useBndHashQualifiers false
    useFeatureHashQualifiers false
    defaultQualifier ''
}

clean {
	delete "${buildDir}/compositeContent.xml"
}

publishP2Repo {
	repoOwner = 'testng-team'
	repoName = 'testng-p2-beta'
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.13'
}
