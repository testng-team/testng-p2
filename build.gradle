buildscript {
	repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
	}
	dependencies {
		classpath 'org.standardout:bnd-platform:1.7.0'
        classpath 'biz.aQute.bnd:biz.aQute.bndlib:4.3.1'
	}
}

def buildTime() {
	def df = new java.text.SimpleDateFormat("yyyyMMddHHmm")
	df.setTimeZone(TimeZone.getTimeZone("UTC"))
	return df.format(new Date())
}

apply plugin: 'org.standardout.bnd-platform'

repositories {
	mavenCentral()
	jcenter()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

// project version is also platform feature version default,
// here uses TestNG version as project version.
version = '7.11.0'

def buildMode = System.getProperty('BUILD_MODE')
def versionWithQualifier

def qualifierPrefix = 'b'       // 'b' for Beta
if (buildMode == "release") {
    assert !version.endsWith('-SNAPSHOT') : "Please don't use snapshot version ${version} for release"
    qualifierPrefix = 'r'       // 'r' for Release
}

if (version ==~ /^\d+[\.\d+]+$/) {
    versionWithQualifier = version + "." + qualifierPrefix + buildTime()
}
else {
    versionWithQualifier = version.substring(0, version.indexOf('-')) + "." + qualifierPrefix + buildTime()
}

defaultTasks 'updateSiteZip'

updateSiteZip.dependsOn clean

platform {
    bundle "org.testng:testng:${version}", {
        bnd {
            // override/set the symbolic name
            symbolicName = 'org.testng'
            // override/set the bundle name
            bundleName = 'testng'
            // instruction 'Export-Package', "org.testng.*;version=$version"
            instruction 'Import-Package', 
                                            'com.beust.jcommander.*;version="[1.8.3,2.0.0)",' + 
                                            'org.slf4j.*,' + 
                                            'bsh.*;version="[2.0.0,3.0.0)";resolution:=optional,' +
                                            'com.google.inject.*;version="5.0";resolution:=optional,' +
                                            'junit.framework;version="[3.8.1, 5.0.0)";resolution:=optional,' +
                                            'org.junit.*;resolution:=optional,' +
                                            'org.apache.tools.ant.*;version="[1.7.0, 2.0.0)";resolution:=optional,' +
                                            'org.yaml.*;version="[2.0,3.0)";resolution:=optional,' +
                                            '!com.beust.testng,' +
                                            '!org.testng.*,' +
                                            '!com.sun.*'
            instruction 'Require-Bundle', 'com.beust.jcommander;bundle-version="[1.8.3,2.0.0)";visibility:=reexport'
            instruction 'Bundle-Vendor', 'TestNG Team'
            version = versionWithQualifier
        }
        exclude module: 'ant'
        exclude module: 'bsh'
        exclude module: 'junit'
        exclude module: 'jsr305'
        exclude module: 'guice'
        exclude module: 'slf4j-api'
    }

    bundle "org.yaml:snakeyaml:2.2"

    bundle "org.jcommander:jcommander:1.83", {
        bnd {
            // override/set the symbolic name
            symbolicName = 'com.beust.jcommander'
            // override/set the bundle name
            bundleName = 'jcommander'
        }
    }

    featureId 'org.testng.p2.feature'
    featureName 'TestNG P2 Feature'
    featureVersion versionWithQualifier
    featureProvider 'TestNG Team'

    categoryName 'TestNG P2 Libraries'
    categoryId 'org.testng.p2.libraries'

    useBndHashQualifiers false
    useFeatureHashQualifiers false
    defaultQualifier ''

    downloadsDir = new File(System.properties['user.home'], '.bnd-eclipse')
    eclipseMirror = 'http://philippkatz.de/eclipse-p2-minimal.tar.gz'
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.13'
}
