buildscript {
	repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'https://dl.bintray.com/missedone/gradle-plugins/'
        }
	}
	dependencies {
		classpath 'org.standardout:bnd-platform:1.4.0'

        // see https://github.com/michel-kraemer/gradle-download-task/issues/56
        classpath 'de.undercouch:gradle-download-task:3.1.1'
        classpath 'org.apache.httpcomponents:httpclient:4.5.2'

        classpath 'com.github.missedone:gradle-bintray-p2-plugin:1.0.1'
	}
}

def buildTime() {
	def df = new java.text.SimpleDateFormat("yyyyMMddHHmmss")
	df.setTimeZone(TimeZone.getTimeZone("UTC"))
	return df.format(new Date())
}

apply plugin: 'org.standardout.bnd-platform'
apply plugin: 'bintray-p2'

repositories {
	mavenCentral()
	jcenter()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

// project version is also platform feature version default,
// here uses TestNG version as project version.
version = '6.9.13-SNAPSHOT'

def versionWithQualifier = version.replaceAll('-SNAPSHOT', "." + buildTime())

defaultTasks 'updateSiteZip'

updateSiteZip.dependsOn clean

platform {
    override {
        optionalImport 'javax.*'
    }
    bundle "org.testng:testng:${version}", {
        bnd {
            optionalImport 'com.google.common.*'
            optionalImport 'org.w3c.*'
            optionalImport 'org.xml.*'
            version = versionWithQualifier
        }
        exclude module: 'ant'
    }
    bnd (name: 'bsh') {
        optionalImport 'org.apache.bsf.*'
    }

    featureId 'org.testng.p2.feature'
    featureName 'TestNG P2 Feature'
    featureVersion versionWithQualifier

    categoryName 'TestNG P2 Libraries'
    categoryId 'org.testng.p2.libraries'

    useBndHashQualifiers false
    useFeatureHashQualifiers false
    defaultQualifier ''
}

publishP2Repo {
	repoOwner = 'testng-team'
	repoName = 'testng-p2-beta'
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.13'
}
